apply plugin: 'com.android.library'
apply plugin: 'io.github.tubb.multichannel'

android {
    compileSdkVersion project.ext.compileSdkVersion
    defaultConfig {
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode project.ext.versionCode
        versionName project.ext.versionName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    project.android.flavorDimensions("default")
    productFlavors {
        dev {
            dimension 'default'
            manifestPlaceholders = [ channel:"dev" ]
        }
    }

}

appChannel {
    channelFlavorConfig {
        channelConfigFilePath "${project.getProjectDir()}/channels.txt"
        configProductFlavor { name ->
            if ('qq'.equals(name)) {
                return {
                    dimension 'default'
                    manifestPlaceholders = [ channel:"${name}_yyb" ]
                }
            } else {
                return {
                    dimension 'default'
                    manifestPlaceholders = [ channel:name ]
                }
            }
        }
        // must call after configProductFlavor dsl
        createChannel()
    }
    bundleOutputConfig {
        outputDir "${project.getProjectDir()}/channels"
        renameBundleFile { project, variant ->
            project.name + '-' + variant.flavorName + '-' + variant.buildType.name +'-'+ project.ext.versionName +'.aar'
        }
    }
    taskConfig {
        disableLintTask true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}
